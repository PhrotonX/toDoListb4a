Build1=Default,com.cm.todolist
File1=3528206.png
File10=emptytasks.bal
File11=folder.png
File12=helpitems.bal
File13=ic_launcher.png
File14=ic_launcher_xl.png
File15=Layout.bal
File16=newfragment.bal
File17=pencil.png
File18=separatorlayout.bal
File19=settingsabout.bal
File2=486023921_623831243812785_3842496593750662642_n.jpg
File20=settingshelp.bal
File21=settingslayout.bal
File22=solid_white.png
File23=sviewlayout.bal
File24=TaskItemLayout.bal
File25=taskviewlayout.bal
File26=taskviewscrolllayout.bal
File3=aboutitems.bal
File4=AttachmentItemLayout.bal
File5=canvas.bal
File6=draweritems.bal
File7=drawerlayout.bal
File8=EditorLayout.bal
File9=EditorScrollLayout.bal
FileGroup1=Default Group
FileGroup10=Default Group
FileGroup11=Default Group
FileGroup12=Default Group
FileGroup13=Default Group
FileGroup14=Default Group
FileGroup15=Default Group
FileGroup16=Default Group
FileGroup17=Default Group
FileGroup18=Default Group
FileGroup19=Default Group
FileGroup2=Default Group
FileGroup20=Default Group
FileGroup21=Default Group
FileGroup22=Default Group
FileGroup23=Default Group
FileGroup24=Default Group
FileGroup25=Default Group
FileGroup26=Default Group
FileGroup3=Default Group
FileGroup4=Default Group
FileGroup5=Default Group
FileGroup6=Default Group
FileGroup7=Default Group
FileGroup8=Default Group
FileGroup9=Default Group
Group=Default Group
Library1=animation
Library10=xcustomlistview
Library11=xui
Library2=b4xdrawer
Library3=contentresolver
Library4=core
Library5=javaobject
Library6=nb6
Library7=phone
Library8=reflection
Library9=sql
ManifestCode='This code will be applied to the manifest file during compilation.~\n~'You do not need to modify it in most cases.~\n~'See this link for for more information: http://www.b4x.com/forum/showthread.php?p=78136~\n~AddManifestText(~\n~<uses-sdk android:minSdkVersion="24"~\n~android:targetSdkVersion="34" />~\n~<supports-screens android:largeScreens="true" ~\n~	android:debuggable="true"~\n~    android:normalScreens="true" ~\n~    android:smallScreens="true" ~\n~    android:anyDensity="true"/>)~\n~SetApplicationAttribute(android:icon, "@drawable/icon")~\n~SetApplicationAttribute(android:label, "$LABEL$")~\n~AddManifestText(~\n~<uses-permission~\n~  android:name="android.permission.WRITE_EXTERNAL_STORAGE"~\n~  android:maxSdkVersion="18" />~\n~)~\n~AddManifestText(~\n~<uses-permission~\n~  android:name="android.permission.READ_EXTERNAL_STORAGE"~\n~  android:maxSdkVersion="18" />~\n~)~\n~AddManifestText(~\n~<uses-permission~\n~  android:name="android.permission.MANAGE_EXTERNAL_STORAGE"~\n~  android:maxSdkVersion="18" />~\n~)~\n~~\n~CreateResource(xml, provider_paths,~\n~    <paths xmlns:android="http://schemas.android.com/apk/res/android">~\n~    <external-path name="root" path="" />~\n~</paths>~\n~)~\n~#AdditionalJar: com.android.support:support-core-utils~\n~'End of default text.~\n~
Module1=Attachment
Module10=DaysOfTheWeek
Module11=Dimen
Module12=EditorActivity
Module13=FileUtils
Module14=Group
Module15=GroupDao
Module16=GroupRepository
Module17=GroupViewHolder
Module18=GroupViewModel
Module19=SettingsAbout
Module2=AttachmentDao
Module20=SettingsActivity
Module21=SettingsHelp
Module22=Starter
Module23=SubGroupViewHolder
Module24=TaskDao
Module25=TaskRepository
Module26=TaskViewerActivity
Module27=TaskViewHolder
Module28=TaskViewModel
Module29=Theme
Module3=AttachmentFileRepository
Module30=Time
Module31=ToDo
Module32=ToDoDatabase
Module33=ToDoFileSystem
Module4=AttachmentRepository
Module5=AttachmentViewHolder
Module6=AttachmentViewModel
Module7=DatabaseUtils
Module8=Date
Module9=DateAndTime
NumberOfFiles=26
NumberOfLibraries=11
NumberOfModules=33
Version=13.1
@EndOfDesignText@
#Region  Project Attributes 
	#ApplicationLabel: To Do List
	#VersionCode: 9
	#VersionName: v2.0.0.2 alpha
	'SupportedOrientations possible values: unspecified, landscape or portrait.
	#SupportedOrientations: unspecified
	#CanInstallToExternalStorage: False
#End Region

#Region  Activity Attributes 
	#FullScreen: False
	#IncludeTitle: False
#End Region

Sub Process_Globals
	'These global variables will be declared once when the application starts.
	'These variables can be accessed from all modules.
	Private xui As XUI
End Sub

Sub Globals
	'These global variables will be redeclared each time the activity is created.
	Private ToDoList As CustomListView
	Private chkToDoItem As CheckBox
	Private toDoItemInfo As Label
	Private menuBtn As Button
	Private btnAdd As Button
	Private pnlClickArea As Panel
	Private hasMoved As Boolean
	Private titleLabel As Label
	Private drawer As B4XDrawer
	Private menuListView As ListView
	Dim menuListViewItemsize As ListView
	Private pnlMenuBgTouch As Panel
	Private clvItems As CustomListView
	
	Dim inMain As Boolean = True
	Private mainFragment As Panel
	Public lastClickTime As Long = 0
	
	Private m_mode As String
	
	Public Const TASK_CHECKED_ROOT_COLOR As Int = Colors.RGB(235, 235, 235)
	Public Const TASK_CHECKED_TEXT_COLOR As Int = Colors.RGB(175, 175, 175)
	Public Const TASK_UNCHECKED_TEXT_COLOR As Int = Colors.RGB(0, 0, 0)
	Public Const TASK_UNCHECKED_ROOT_COLOR As Int = Colors.RGB(255, 255, 255)	
	
	Private pnlSearch As Panel
	Private pnlTitleBar As Panel
	Private searchFragment As Panel
	Private separator As Panel
	Private groupPanel As Panel
	Private lblGroupIcon As Label
	Private lblGroupName As Label
	
	Private smartLists(5) As Group
	
	Private Const SMART_LIST_NONE As Int = -1
	Private Const SMART_LIST_MY_DAY As Int = 0
	Private Const SMART_LIST_PLANNED As Int = 1
	Private Const SMART_LIST_COMPLETED As Int = 2
	Private Const SMART_LIST_ALL As Int = 3
	Private Const SMART_LIST_TASKS As Int = 5
	Private Const SMART_LIST_TRASH As Int = 6
	Private Const SMART_LIST_SEARCH As Int = 7
	Private Const SMART_LIST_SORT As Int = 8
	
	Private Const SEARCH_BY_NONE As Int = 0
	Private Const SEARCH_BY_TITLE As Int = 1
	Private Const SEARCH_BY_DESCRIPTION As Int = 2
	Private Const SEARCH_BY_ATTACHMENTS As Int = 3
	Private Const SEARCH_BY_DUE_DATE As Int = 4
	Private Const SEARCH_BY_REPEAT As Int = 5
	Private Const SEARCH_BY_PRIORITY As Int = 6
	
	Private Const SEARCH_BY_DUE_DATE_SPECIFIC_DATE As String = "Specific Date"
	Private Const SEARCH_BY_DUE_DATE_SPECIFIC_DATE_RANGE As String = "Specific Date Range"

	Private m_savedSmartList As Int = SMART_LIST_MY_DAY
	Private m_savedGroup As Group
	Private m_savedSearchByDueDateIdentifer = ""
	Private m_savedSearchByDueDateRange1 As Date
	Private m_savedSearchByDueDateRange2 As Date
	Private m_savedSearchByRepeat(7) As Boolean
	Private m_savedSearchByPriority = ""
	Private m_query As String
	
	Private m_itemsLoaded As Boolean = False
	
	Private m_tasks As List
End Sub

Sub Activity_Create(FirstTime As Boolean)
	'ToDoList.AnimationDuration = 0
	
	drawer.Initialize(Me, "Drawer", Activity, 300dip)
	drawer.CenterPanel.LoadLayout("layout")
	mainFragment.LoadLayout("newFragment")
	drawer.LeftPanel.LoadLayout("drawerLayout")
	LoadDrawerItems
	listview_init
	'Activity.LoadLayout("Layout")
	
	m_tasks.Initialize
End Sub

Sub Activity_Resume
	' Remove all items to update data and avoid data duplication.
	ToDoList.Clear
	LoadItems
	
	Starter.CheckInstanceState
End Sub

Sub Activity_Pause (UserClosed As Boolean)

End Sub

Private Sub btnAdd_Click
	' Set the editor mode by submitting the requested value into the InstanceState bundle.
	Starter.InstanceState.Put(Starter.EXTRA_EDITOR_MODE, Starter.EDITOR_MODE_CREATE)
	
	' Start the activity.
	StartActivity(EditorActivity)
End Sub

Private Sub chkToDoItem_CheckedChange(Checked As Boolean)
	' Cancel the function if Sender is null.
	If Sender == Null Then
		Return
	End If
	
	' Retrieve the index of item clicked.
	Dim index As Int = ToDoList.GetItemFromView(Sender)
	
	' Retrieve the viewHolder of item clicked based on the index value.
	Dim viewHolder As TaskViewHolder = ToDoList.GetValue(index)
	
	' Retrieve the item from the database based on the DB-based ID stored in the viewHolder.
	Dim item As ToDo = Starter.TaskViewModelInstance.GetTask(viewHolder.ID)
	
	' Update the checked value of the task.
	item.Done = Checked
	
	' Since the item has been changed, update the database.
	Starter.TaskViewModelInstance.UpdateTask(item)
	
	Select m_savedSmartList:
		Case SMART_LIST_PLANNED:
			If Checked == True Then
				' Hide the completed task if the current smart list is in "Planned"				
				ToDoList.RemoveAt(index)

				' Also remove the subgroup If it exists And the subgroup Is empty.
				Dim prevSubGroup As Object = ToDoList.GetValue(index - 1)
				Dim nextSubGroup As Object
				
				If (index > ToDoList.Size - 1) Then
					nextSubGroup = Null
				Else
					nextSubGroup = ToDoList.GetValue(index)
				End If
				
				If prevSubGroup Is SubGroupViewHolder Then
					If nextSubGroup == Null Then
						ToDoList.RemoveAt(index - 1)
					End If
					If nextSubGroup Is SubGroupViewHolder Then
						ToDoList.RemoveAt(index - 1)
					End If
				End If
				
			End If
		Case SMART_LIST_COMPLETED:
			If Checked == False Then
				' Hide the incomplete task if the current smart list is in "Completed"
				ToDoList.RemoveAt(index)
			End If
		Case Else:
			If Checked == True Then
				ToDoList.RemoveAt(index)
				viewHolder.Root.RemoveViewFromParent
				
				If m_itemsLoaded Then
					ToDoList.InsertAt(ToDoList.Size - 1, viewHolder.Root, viewHolder)
				Else
					ToDoList.Add(viewHolder.Root, viewHolder)
					m_itemsLoaded = True
				End If
			Else
				ToDoList.RemoveAt(index)
				viewHolder.Root.RemoveViewFromParent
				ToDoList.InsertAt(0, viewHolder.Root, viewHolder)
			End If
	End Select
	
	' Add visual indicator that the task has been changed
	If Checked == True Then
		' Make the items return to its original look after the task is marked as
		' incomplete.
		viewHolder.TaskCheckbox.TextColor = TASK_CHECKED_TEXT_COLOR
		viewHolder.TaskInfo.TextColor = TASK_CHECKED_TEXT_COLOR
		viewHolder.Root.Color = TASK_CHECKED_ROOT_COLOR
	Else
		' Make the items looks slightly lighter after task completion.
		viewHolder.TaskCheckbox.TextColor = TASK_UNCHECKED_TEXT_COLOR
		viewHolder.TaskInfo.TextColor = TASK_UNCHECKED_TEXT_COLOR
		viewHolder.Root.Color = TASK_UNCHECKED_ROOT_COLOR
	End If
End Sub

' Creates and returns a panel for items loaded into the ToDoList.
Private Sub CreateItem(viewHolder As TaskViewHolder) As B4XView
	' Create a root panel
	Dim panel As B4XView = xui.CreatePanel("")
	
	' Set properties into the root panel
	panel.SetLayoutAnimated(0, 0, 0, 100%x, 70dip)
	panel.LoadLayout("TaskItemLayout")
	panel.SetColorAndBorder(Colors.White, 0, Colors.White, 0)
	
	' Bind the root panel
	viewHolder.Root = panel
	
	Return panel
End Sub

' Binds task data into the task view.
Private Sub BindItem(item As ToDo, viewHolder As TaskViewHolder)
	' Bind the checkbox
	viewHolder.TaskCheckbox = chkToDoItem
	' Bind the view for task info
	viewHolder.TaskInfo = toDoItemInfo
	' Bind the task title
	viewHolder.TaskCheckbox.Text = item.GetTitle
	' Bind the glance information
	viewHolder.TaskInfo.Text = item.GetGlance
	' Make a reference to the task_id from the database.
	viewHolder.ID = item.GetId
	' Bind the panel into the view holder.
	viewHolder.TaskPanel = pnlClickArea
	
	' Bind the checked value. Must be placed in this part in order since this
	' triggers the chkToDoItem_CheckedChange event.
	viewHolder.TaskCheckbox.Checked = item.Done
End Sub

' Loads the items.
Private Sub LoadItems()

		m_itemsLoaded = False

	' Initialize a list.
	' Testing code for item sorting and searching.
	' m_tasks = Starter.TaskViewModelInstance.FindTasksByDueDate(DateTime.Now - 86400000, DateTime.Now + (86400000 * 2), True)
	'Dim repeat(7) As Boolean = Array As Boolean(True, False, True, False, False, False, False)
	
	'm_tasks = Starter.TaskViewModelInstance.FindTasksByRepeat(repeat, True)
	
	' Obtain all tasks from the database, depending on the smart list mode.
	Select m_savedSmartList:
		Case SMART_LIST_MY_DAY:
			m_tasks = Starter.TaskViewModelInstance.GetTasksToday()
		Case SMART_LIST_ALL: 
			m_tasks = Starter.TaskViewModelInstance.GetAllTasks()
		Case SMART_LIST_COMPLETED:
			m_tasks = Starter.TaskViewModelInstance.GetTasksCompleted()
		Case SMART_LIST_PLANNED:
			m_tasks = Starter.TaskViewModelInstance.GetTasksPlanned()
		Case SMART_LIST_TRASH:
			m_tasks = Starter.TaskViewModelInstance.GetDeletedTasks()
		Case Else, SMART_LIST_NONE:
			m_tasks = Starter.TaskViewModelInstance.GetGroupedTasks(m_savedGroup.GetID)
	End Select
	
	' Null check the list.
	If m_tasks.IsInitialized Then
		Dim itr As String = ""
		
		' Iterate each item.
		For Each item As ToDo In m_tasks
			' Initialize a ViewHolder
			Dim viewHolder As TaskViewHolder
			viewHolder.Initialize
			
			' Create subgroups.
			Select m_savedSmartList:
				Case SMART_LIST_PLANNED:
					Dim currentDateIdentification As String = item.GetDueDate.IdentifyDate
					
					If itr <> currentDateIdentification And item.Done == False Then
						itr = currentDateIdentification
						
						Dim subGroup As SubGroupViewHolder
						subGroup.Initialize(xui)
						subGroup.Name.Text = currentDateIdentification
						
						ToDoList.Add(subGroup.BuildView(), subGroup)
					End If
				Case SMART_LIST_SEARCH, SMART_LIST_SORT, SMART_LIST_COMPLETED:
				Case Else:
					' If the Next item Is marked As completed, Then add subGroup "Completed"
					If item.Done == True And itr == "" Then
						Dim subGroup As SubGroupViewHolder
						subGroup.Initialize(xui)
						subGroup.Name.Text = "Completed"
						itr = "Completed"
						
						ToDoList.Add(subGroup.BuildView(), subGroup)
					End If
			End Select
			
			' Add items into the list view by binding the viewholder and the data.
			ToDoList.Add(CreateItem(viewHolder), viewHolder)
			
			' Bind the currently referenced vieHolder that ToDoList points to into there
			' currently iterated data.
			BindItem(item, viewHolder)
		Next
	End If
	
	' Add the item count information
	Select m_savedSmartList
		' Planned and Completed has a unique way of counting items.
		Case SMART_LIST_PLANNED, SMART_LIST_COMPLETED:
			' @TODO: Add functionality here...
		Case Else:
			Dim subGroup As SubGroupViewHolder
			subGroup.Initialize(xui)
			
			Dim count As Int = m_tasks.Size
			
			subGroup.Name.Text = count & " task"
			
			' Make the counting information plural if possible.
			If count > 1 Then
				subGroup.Name.Text = subGroup.Name.Text & "(s)"
			End If
			
			ToDoList.Add(subGroup.BuildView(), subGroup)
	End Select
	
End Sub

Sub menuBtn_Click
	If button_handling Then
		pnlMenuBgTouch.BringToFront
		menuListView.SetVisibleAnimated(250, True)
		menuListView.BringToFront
		menuListView.SetLayoutAnimated(250, menuListViewItemsize.Left, menuListViewItemsize.Top, menuListViewItemsize.Width, menuListViewItemsize.Height)
	End If
End Sub

Private Sub pnlMenuBgTouch_Touch (Action As Int, X As Float, Y As Float)
	Select Action
		Case Activity.ACTION_DOWN
			If menuListView.Visible = True Then
				pnlMenuBgTouch.SendToBack
				menuListView.SetLayoutAnimated(250, 1080, 0, 0, 228)
				menuListView.SetVisibleAnimated(500, False)
				
			End If
	End Select
End Sub

Private Sub menuListView_ItemClick (Position As Int, Value As Object)
	Select Position
		Case 0
			pnlMenuBgTouch.SendToBack
			menuListView.SetLayoutAnimated(250, 1080, 0, 0, 228)
			menuListView.SetVisibleAnimated(500, False)
			
			
			
		Case 1
			pnlMenuBgTouch.SendToBack
			menuListView.SetLayoutAnimated(250, 1080, 0, 0, 228)
			menuListView.SetVisibleAnimated(500, False)
			
			
		Case 2
			StartActivity(SettingsActivity)
			pnlMenuBgTouch.SendToBack
			menuListView.SetLayoutAnimated(250, 1080, 0, 0, 228)
			menuListView.SetVisibleAnimated(500, False)
			
	End Select
End Sub



' Opens EditorActivity to edit a clicked task.
Private Sub pnlClickArea_Click
	SetIdAsExtra
	
	' Set the EditorActivity mode into edit mode.
	Starter.InstanceState.Put(Starter.EXTRA_EDITOR_MODE, Starter.EDITOR_MODE_EDIT)
	
	StartActivity(EditorActivity)
End Sub

' Opens the Task Viewer to view a task in detail.
Sub pnlViewArea_Click
	SetIdAsExtra
	
	StartActivity(TaskViewerActivity)
End Sub

' Retrieves the ID and sets the DB-based ID of the clicked task as an extra for use in other
' activities.
Private Sub SetIdAsExtra
	' Obtain the index of the clicked item from the ListView.
	Dim index As Int = ToDoList.GetItemFromView(Sender)
	
	' Obtain the viewHlder that is bound into the view.
	Dim viewHolder As TaskViewHolder = ToDoList.GetValue(index)
	
	' Retrieve the task_id bound into the viewHolder and set that ID into the extras.
	Starter.InstanceState.Put(Starter.EXTRA_EDITOR_TASK_ID, viewHolder.ID)
End Sub


Sub pnlViewArea_Touch (Action As Int, X As Float, Y As Float)
	' Obtains the index of the clciked
	Dim index As Int = ToDoList.GetItemFromView(Sender)
	' gets the viewholder
	Dim viewHolder As TaskViewHolder = ToDoList.GetValue(index)
	
	
	Select Action
		Case Activity.ACTION_DOWN ' sets bgcolor of pnl to gray if held
			hasMoved = False
			
			' Set the main click color.
			viewHolder.Root.SetColorAnimated(50, Colors.White, Colors.Gray)
            
		Case Activity.ACTION_MOVE 'sets bgcolor of pnl to white or gray if touch is held and moved
			If Not(hasMoved) Then
				hasMoved = True
				
				' Process click style.
				OnChkboxViewAreaClick(100, viewHolder)
				
			End If
            
		Case Activity.ACTION_UP 'starts activity if touch is released
			If Not(hasMoved) Then
				hasMoved = True
				
				' Process click style.
				OnChkboxViewAreaClick(100, viewHolder)
				
				' Retrieve the task_id bound into the viewHolder and set that ID into the extras.
				Starter.InstanceState.Put(Starter.EXTRA_EDITOR_TASK_ID, viewHolder.ID)
				
				StartActivity(TaskViewerActivity)
			End If
		
		Case 3
			' only processes if touch has not moved yet 
			If Not(hasMoved) Then
				hasMoved = True
				
				' Process click style.
				OnChkboxViewAreaClick(50, viewHolder)
				
			End If
			
		
       End Select
End Sub

' Used to process the click style of panels above the checkbox of tasks.
Public Sub OnChkboxViewAreaClick(percentage As Int, viewHolder As TaskViewHolder)
	' Touch color differs whether the item is marked as complete or not.
	If viewHolder.TaskCheckbox.Checked Then
		' Execute if task is marked as complete
		viewHolder.Root.SetColorAnimated(percentage, Colors.Gray, TASK_CHECKED_ROOT_COLOR)
	Else
		' Execute if task is not marked as complete
		viewHolder.Root.SetColorAnimated(percentage, Colors.Gray, TASK_UNCHECKED_ROOT_COLOR)
	End If
End Sub

Private Sub drawerButton_Click
	If button_handling Then
		drawer.LeftOpen = True
	End If
End Sub

Private Sub listview_init
		menuListView.Visible = False
		menuListView.AddSingleLine2("	Refresh", 1)
		menuListView.AddSingleLine2("	Sort", 2)
		menuListView.AddSingleLine2("	Settings", 3)
		menuListView.SingleLineLayout.Label.TextSize = 15
		menuListView.SingleLineLayout.Label.TextColor = Colors.Black
		menuListView.SingleLineLayout.ItemHeight = 43dip
		
		Dim cd As ColorDrawable
		cd.Initialize(Colors.White, 3dip)
		
		menuListView.Background = cd
		
		
		menuListViewItemsize.Initialize("")
		menuListViewItemsize.SetLayout(menuListView.Left, menuListView.Top, menuListView.Width, menuListView.Height)
		
		menuListView.SetLayoutAnimated(200,100%x, 0%y, 0, 114dip)
		pnlMenuBgTouch.SendToBack
		
		Dim javaobj As JavaObject = menuListView
		javaobj.RunMethod("setDivider", Array(Null))
		javaobj.RunMethod("setDividerHeight", Array(0))
End Sub

Sub LoadDrawerItems
	clvItems.sv.ScrollViewInnerPanel.Color = xui.Color_Transparent
	clvItems.sv.Color = xui.Color_Transparent
	
	Dim items As List
	items.Initialize
	
	For Each obj As Group In smartLists		
		obj.Initialize(0)
	Next
	
	smartLists(0).SetTitle("My Day")
	smartLists(0).SetIcon("")
	
	smartLists(1).SetTitle("Planned")
	smartLists(1).SetIcon("")
	
	smartLists(2).SetTitle("Completed")
	smartLists(2).SetIcon("")
	
	smartLists(3).SetTitle("All")
	smartLists(3).SetIcon("")
	
	smartLists(4).SetTitle("Trash")
	smartLists(4).SetIcon("")

	For Each obj As Group In smartLists
		items.Add(obj)
	Next

	OnLoadTaskGroups(items)
	
	
	Dim sepPanel As B4XView = xui.CreatePanel("")
	sepPanel.SetLayoutAnimated(0, 0, 0, 100%x, 2dip) ' 2dip height for the separator
	Dim innerSep As B4XView = xui.CreatePanel("")
	innerSep.SetLayoutAnimated(0, 5%x, 0, 45.56%x, 2dip) ' 80% width, centered (10% margin on each side)
	innerSep.Color = xui.Color_LightGray
	sepPanel.AddView(innerSep, 5%x, 0, 45.56%x, 2dip)
	sepPanel.Enabled = False
	innerSep.Enabled = False	
	
	Dim separatorViewHolder As GroupViewHolder
	separatorViewHolder.Initialize
	separatorViewHolder.ID = 0
	separatorViewHolder.IsSeparator = True
	clvItems.Add(sepPanel, separatorViewHolder)
	
	' Load task groups from the database
	items.Clear
	For Each groupObj As Group In Starter.GroupViewModelInstance.GetGroups()
		groupObj.SetIcon("")
		items.Add(groupObj)
	Next
	
	OnLoadTaskGroups(items)

	Log(Activity.Width)
	Log(drawer.LeftPanel.Width)
	
End Sub

Private Sub OnLoadTaskGroups(items As List)
	For Each item As Group In items		    
		Dim p As B4XView = xui.CreatePanel("")
		p.SetLayoutAnimated(0, 0, 0, 100%x, 60dip)
		p.LoadLayout("drawerItems")
		
		Dim viewHolder As GroupViewHolder
		viewHolder.Initialize
		
		viewHolder.Root = p
		viewHolder.ID = item.GetID
		viewHolder.Name = lblGroupName
		viewHolder.Icon = lblGroupIcon
		viewHolder.BackPanel = groupPanel
		
		viewHolder.Name.Text = item.GetTitle
		viewHolder.Icon.Text = item.GetIcon

		viewHolder.Root.SetColorAndBorder(Colors.White, 0, Colors.Transparent, 0)
		viewHolder.BackPanel.SetColorAndBorder(Colors.White, 0, Colors.Transparent, 0)
		viewHolder.Icon.SetColorAndBorder(Colors.White, 0, Colors.Transparent, 0)
		viewHolder.Name.SetColorAndBorder(Colors.White, 0, Colors.Transparent, 0)
		clvItems.Add(p, viewHolder)
	Next
End Sub

' Function for task group click.
Private Sub clvItems_ItemClick (Index As Int, Value As Object)
	Dim viewHolder As GroupViewHolder = Value
	
	If viewHolder.IsSeparator == True Then
		' Reset selection to previous item
		clvItems.GetPanel(Index).Color = xui.Color_Transparent
		Return
	End If
	
	' 0 ID for groups cannot exist on the database. Therefore, it is reserved
	' for special groups such as smart lists.
	If viewHolder.ID == 0 Then
		m_savedSmartList = Index
		
		OnLoadPage(smartLists(Index))
	Else
		m_savedSmartList = SMART_LIST_NONE
		
		' Get the associated group based on the ID stored in the clicked view holder of the nav drawer.
		Dim groupObj As Group = Starter.GroupViewModelInstance.GetGroup(viewHolder.ID)
		
		OnLoadPage(groupObj)
	End If
End Sub

Private Sub OnLoadPage(item As Group)
	m_savedGroup = item
	
	titleLabel.Text = item.GetTitle
	drawer.LeftOpen = False
	mainFragment.RemoveAllViews
	mainFragment.LoadLayout("newFragment")
	LoadItems
	drawer.LeftOpen = False
End Sub

Private Sub searchBtn_Click
	If button_handling Then
		pnlSearch.SetLayout(pnlTitleBar.Left, pnlTitleBar.Top, 100%x, pnlTitleBar.Height)
		pnlSearch.SetVisibleAnimated(150, True)
		pnlSearch.BringToFront
		
		searchFragment.SetLayout(mainFragment.Left, mainFragment.Top, mainFragment.Width, mainFragment.Height)
		searchFragment.SetVisibleAnimated(150, True)
		searchFragment.BringToFront
		
		mainFragment.Visible = False
		pnlTitleBar.Visible = False
	End If
End Sub

Private Sub backBtn_Click
	If button_handling Then
		Dim fadeout As Animation
		fadeout.InitializeAlpha("fade", 1, 0)
		fadeout.Duration = 150
		
		mainFragment.Visible = True
		pnlTitleBar.Visible = True
		
		fadeout.Start(pnlSearch)
		fadeout.Start(searchFragment)
		
		pnlSearch.Visible = False
		searchFragment.Visible = False
		
		pnlSearch.SendToBack
		searchFragment.SendToBack
	End If
End Sub

Public Sub button_handling As Boolean
	Dim now As Long = DateTime.Now
	If now - lastClickTime < 500 Then
		' Less than 300ms since last click, ignore
		Return False
	End If
	lastClickTime = now ' Update the time of the last click
	Return True
End Sub

Private Sub enterBtn_Click
	ToastMessageShow("startactivity", False)
	'StartActivity(searchactivity)
End Sub
